
macro do_fft gr
  fftf($1.chi, kmin=3.0, kmax=13.5, dk=1.5, kweight=2)
  set $1.chik = $1.chi * $1.k^2
end macro

macro do_path 1  path1
  ff2chi($1, group = $2)
  do_fft $2
end macro


# read chi(k) data
read_data(file = 'feo_data.chi', group=data, label  = 'k chi chi_kw')

# define fitting variables  
guess de0     = 0.0    # move energy origin of calculation to match data
set   amp     = 0.75    # overall amplitude of calculation
guess n1      = 6.0    # Fe-O coordination
guess n2      =12.0    # Fe-Fe coordination

guess dr1     = 0.0    # shift of R -- neighbor distance
guess dr2     = 0.0    # shift of R -- neighbor distance
guess sig1    = 0.0    # add a mean-square-disorder term
guess sig2    = 0.0    # add a mean-square-disorder term


# define theoretical XAFS paths for Fe:

path(index = 1, file = feff/fe_o_214.dat, 
     label = 'FeO 1st neighbor (6 O @ 2.14A)',
     s02   = amp * n1, 
     delr  = dr1, 
     sigma2= sig1, 
     e0    = de0)

path(index = 2, file = feff/fe_o_303.dat, 
     label = 'FeO 2nd neighbor (12 Fe @ 3.03A)',
     s02   = amp * n2, 
     delr  = dr2, 
     sigma2= sig2, 
     e0    = de0)

#
do_fft data

newplot (data.r, data.chir_mag, key='data',
	 xlabel = 'R (\A)', 
          ylabel = '|\gx(R)| (\A\u-3\d)')
	

feffit(chi= data.chi, 1,2,  group=fit, 
       rmin = 1.1, rmax = 3.2,
       kmin=3.0, kmax=13.5, dk=1.5, kweight=2)

do_fft fit
plot (fit.r, fit.chir_mag, key='best-fit')

#  (create path1.k and path1.chi)
do_path 1 path1
do_path 2 path2

newplot ( data.r,  data.chir_mag, key='data')
plot    (  fit.r,   fit.chir_mag, key='best-fit')
plot    (path1.r, path1.chir_mag, key='path 1')
plot    (path2.r, path2.chir_mag, key='path 2')

echo "Best fit values for the fitted variables:"

show @variables
$title = "data: k, chik, win, fit_k, fit_chik, path1, path2"
set p1.chik = path1.chik-1.5
set p2.chik = path2.chik-2.0
write_data(file=fe_kfit2.dat,  data.k, data.chik, data.win, fit.k,fit.chik,
                               p1.chik, p2.chik, $title)

$title = "data: r, chi, fit, path1, path2"
write_data(file=fe_rfit2.dat,  data.r, data.chir_mag,  fit.chir_mag,
                               path1.chir_mag, path2.chir_mag, 
                               data.chir_re,  fit.chir_re, $title)
